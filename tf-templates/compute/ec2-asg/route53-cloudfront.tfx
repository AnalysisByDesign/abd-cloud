# --------------------------------------------------------------------------------------------
# Route53 Entries
#
# Inbound HTTP/s requests go like this...
# - http://abd-wordpress.uk/        -> CloudFront 
# - CloudFront http                 -> CloudFront https://abd-wordpress.uk/
# - https://abd-wordpress.uk/       -> CloudFront
# - CloudFront https                -> http://origin.abd-wordpress.uk/
# - http://origin.abd-wordpress.uk/ -> http://web.abd-wordpress.uk/
# - http://web.abd-wordpress.uk/    -> Load Balancer
#
# This means we can redirect traffic at either of the two following points
# to a maintenance page if required.
# - http://abd-wordpress.uk/
# - http://origin.abd-wordpress.uk/ 
# Just by changing the DNS destination for these records
#
# --------------------------------------------------------------------------------------------

# CloudFront frontend traffic - http(s)://abd-wordpress.uk/ -> CloudFront
module "dns_cloudfront" {
  source = "git@github.com:AnalysisByDesign/abd-cloud-modules.git//network/dns/alias-record"

  zone_id                      = "${data.aws_route53_zone.public.zone_id}"
  name                         = ""
  alias_name                   = "${aws_cloudfront_distribution.cloudfront.domain_name}"
  alias_zone_id                = "${aws_cloudfront_distribution.cloudfront.hosted_zone_id}"
  alias_evaluate_target_health = true
}

# CloudFront backend traffic - http://origin.abd-wordpress.uk/ -> http://web.abd-wordpress.uk/
module "dns_cloudfront-origin" {
  source = "git@github.com:AnalysisByDesign/abd-cloud-modules.git//network/dns/record"

  zone_id = "${data.aws_route53_zone.public.zone_id}"
  name    = "origin"
  type    = "CNAME"
  ttl     = "30"
  records = ["web.${var.public_apex_domain}"]
}

# Load Balancer frontend traffic - http://web.abd-wordpress.uk/ -> Load Balancer
module "dns_load_balancer" {
  source = "git@github.com:AnalysisByDesign/abd-cloud-modules.git//network/dns/alias-record"

  zone_id                      = "${data.aws_route53_zone.public.zone_id}"
  name                         = "web"
  alias_name                   = "${module.load_balancer.alb_dns_name}"
  alias_zone_id                = "${module.load_balancer.alb_zone_id}"
  alias_evaluate_target_health = false
}
